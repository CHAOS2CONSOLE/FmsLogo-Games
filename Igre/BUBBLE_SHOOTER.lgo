to bubble_shooter [:n 5]
	st cs ct ss
	PR [Dobro došli u BUBBLE SHOOTER !]
	PR [Očekujte dobru zabavu !]
	PR [-----------------------------------------]
	wait 100
	PR [PRAVILA IGRE]
	PR [Ako želite naciljati na balon kliknite na njega]
	PR [Odmah će se ostali baloni uokolo probušiti uz njega]
	PR [Nemožeš probušiti balone koje imaju balone ispod sebe]
	wait 100 
	PR [-----------------------------------------]
	PR [Neka igra započne !]
	PR [-----------------------------------------]
	make "n 5
	MOUSEON [pritisnut] [] [] [] []
	napravi_balone :n
	make "bodovi 0 
	nacrtaj :n 
	
end 

to kvadrat 
	lt 90 fd 250/2 
	seth 0 
	repeat 2 [fd 490 rt 90 fd 250 rt 90]
end

to nacrtaj :n
	cs
	setpc "black
	pd kvadrat fd 490
	bk 12.5 rt 90 pu fd 12.5 seth 0 
	pd nacrtaj_balone :n 
	pu home pd 
end

to nacrtaj_balone :n 
	setpc "black
	seth 90
	localmake "startX xcor
	localmake "ima_balon "false
	repeat :n [
		localmake "tn repcount
		repeat 10 [
			localmake "trenutni (:tn-1)*10+repcount
			localmake "b (item :trenutni :baloni)
			if (:b > -1) [
				make "ima_balon "true
				setfc (item :b :boja)
				circle 12.5
				fill 
			]
			pu fd 25 pd
		]
		pu setxy :startX ycor-25 pd
	]
	if (not :ima_balon) [
		pr [POBJEDIO SI!!!]
		pr (se [Imas] :bodova [bodova!!!])
		MOUSEOFF
	]
end 

to napravi_balone :n
	make "boja [[255 0 0] [0 128 0] [0 0 255] [128 0 128]] ;[red green blue purple]
	localmake "m (count :boja)
	make "baloni []
	repeat :n*10 [
		(rerandom (random 100000))
		localmake "rand (random :m)+1
		make "baloni lput :rand :baloni
	]
end

to pritisnut
	localmake "mp mousepos
	pu setpos :mp
	if (or pixel = [0 0 0] pixel = [255 255 255]) [stop]
	seth 0
	pu bk 25
	if (not or pixel = [0 0 0] pixel = [255 255 255]) [stop]
	fd 25
	
	localmake "tb pixel
	localmake "x (int ((first :mp)+125)/25)+1
	localmake "y int (abs (last :mp)-490)/25
	
	localmake "bfs (list (list :x :y))
	make "visited []
	while [(count :bfs) > 0] [
		localmake "pos pop "bfs
		if (not memberp :pos :visited) [
			make "visited lput :pos :visited
			
			localmake "x (first :pos)
			localmake "y (last :pos)
			
			
			localmake "i :x+10*:y
			if (item :i :baloni) > -1 [
				if (item (item :i :baloni) :boja) = :tb [

					make "baloni (listtoarray :baloni)
					setitem :i :baloni -1
					make "baloni (arraytolist :baloni)
					
					make "bodovi :bodovi+5
					if (:x > 1) [push "bfs (list :x-1 :y)]
					if (:x < 10) [push "bfs (list :x+1 :y)]
					if (:y > 0) [push "bfs (list :x :y-1)]
					if (:y < :n-1) [push "bfs (list :x :y+1)]
				]
			]
		]
	]
	pr (se [Bodovi su ti:] :bodovi)
	nacrtaj :n
end 

Make "baloni [-1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1 -1]
Make "bodovi 250
Make "boja [[255 0 0] [0 128 0] [0 0 255] [128 0 128]]
Make "boje [red green blue purple]
Make "c 1
Make "color [0 128 0]
Make "cz 0
Make "l []
Make "n 5
Make "p [-112.499999 452.499999]
Make "slucajno "blue
Make "startup [bubble_shooter]
Make "visited [[9 0] [9 1] [10 0] [8 0]]
